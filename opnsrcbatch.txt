@echo off
setlocal enabledelayedexpansion

rem Set color variables
set "red=[91m"
set "green=[92m"
set "yellow=[93m"
set "blue=[94m"
set "reset=[0m"

:main
cls
type banner.txt
echo.

echo %green%1. Sync to IFP%reset%
echo %blue%2. Download InstallForge%reset%
echo %yellow%3. Go to PMI Documentation%reset%
echo %red%4. Exit%reset%

choice /c 1234 /n /m "Enter your choice: "
set "choice=%errorlevel%"

if "%choice%"=="1" (
    call :syncToIFP
) else if "%choice%"=="2" (
    call :downloadInstallForge
) else if "%choice%"=="3" (
    call :goToPMIDocumentation
) else if "%choice%"=="4" (
    echo Exiting...
    timeout /t 2 /nobreak >nul
    exit /b 0
) else (
    echo Invalid choice. Please try again.
    timeout /t 2 /nobreak >nul
    goto :main
)

goto :main

:syncToIFP
rem Check if the user has already synced to IFP
if not exist "1.20.1.ifp" (
    echo You haven't synced to IFP yet.
    echo Your PMI build won't function if you don't do a sync to IFP first.
    timeout /t 3 /nobreak >nul
    goto :main
)

rem Get the user's Windows username and use it for replacing "ph7n"
for /f "delims=" %%a in ('whoami') do set "windowsUsername=%%~na"
powershell -command "(Get-Content '1.20.1.ifp') -replace 'ph7n', '!windowsUsername!' | Set-Content '1.20.1.ifp'"
echo Sync to IFP completed!
timeout /t 3 /nobreak >nul
goto :main

:downloadInstallForge
echo Downloading InstallForge...
powershell.exe -Command "& { $url='https://installforge.net/downloads/?i=IFSetup'; $output='InstallForge.exe'; (New-Object Net.WebClient).DownloadFile($url, $output) }"
if %errorlevel% neq 0 (
    echo Failed to download InstallForge.
    timeout /t 2 /nobreak >nul
) else (
    echo Download completed!
    timeout /t 5 >nul
)
goto :eof

:goToPMIDocumentation
echo Opening PMI Documentation...
start "" "https://phmi.tk/docs"
goto :eof
