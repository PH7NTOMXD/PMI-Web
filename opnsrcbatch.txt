@echo off
setlocal enabledelayedexpansion

rem Set color variables
set "red=[91m"
set "green=[92m"
set "yellow=[93m"
set "blue=[94m"
set "reset=[0m"

:main
cls
type banner.txt
echo.

echo %green%1. Download InstallForge%reset%
echo %yellow%2. Go to PMI Documentation%reset%
echo %red%3. Exit%reset%
echo %blue%4. Unzip Source-Code%reset%

choice /c 1234 /n /m "Enter your choice: "
set "choice=%errorlevel%"

if "%choice%"=="1" (
    call :downloadInstallForge
) else if "%choice%"=="2" (
    call :goToPMIDocumentation
) else if "%choice%"=="3" (
    goto :eof
) else if "%choice%"=="4" (
    call :unzipSourceCode
) else (
    echo Invalid choice. Please try again.
    timeout /t 2 /nobreak >nul
    goto :main
)

goto :main

:downloadInstallForge
echo Downloading InstallForge...
powershell.exe -Command "& { $url='https://installforge.net/downloads/?i=IFSetup'; $output='InstallForge.exe'; (New-Object Net.WebClient).DownloadFile($url, $output) }"
if %errorlevel% neq 0 (
    echo Failed to download InstallForge.
    timeout /t 2 /nobreak >nul
) else (
    echo Download completed!
    timeout /t 5 >nul
)
goto :eof

:goToPMIDocumentation
echo Opening PMI Documentation...
start "" "https://phmi.tk/docs"
goto :eof

:unzipSourceCode
echo Unzipping Source-Code...
call powershell.exe -Command "Expand-Archive -Path 'PMI-SRC.zip' -DestinationPath '.' -Force"
if %errorlevel% neq 0 (
    echo Failed to unzip Source-Code.
    timeout /t 2 /nobreak >nul
) else (
    echo Unzip completed!
)

rem Delete the zip file
del /f /q "PMI-SRC.zip"
echo Existing zip file deleted.
timeout /t 5 >nul
goto :eof
